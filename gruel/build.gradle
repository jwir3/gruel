apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.squareup.retrofit:retrofit:1.6.1'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'io.reactivex:rxjava-debug:1.0.1'
    compile 'net.rcarz:jira-client:0.5'
}

sourceCompatibility=1.7

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
  archives jar

  archives javadocJar
  archives sourcesJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "${group}"
            artifactId 'gruel'
            version "${version}"
            artifact jar
            from components.java
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
               name 'gruel'
               packaging 'jar'
               description 'Gruel is a gradle plugin containing a set of tools commonly used in multiple projects'
               url 'http://github.com/jwir3/gruel'

               scm {
                   url 'scm:git@github.com:jwir3/gruel.git'
                   connection 'scm:git@github.com:jwir3/gruel.git'
                   developerConnection 'scm:git@github.com:jwir3/gruel.git'
               }

               licenses {
                   license {
                       name 'Mozilla Public License Version 2.0'
                       url 'https://www.mozilla.org/en-US/MPL/2.0/'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'jwir3'
                       name 'Scott Johnson'
                   }
               }
           }
        }
    }
}
